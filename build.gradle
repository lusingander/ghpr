plugins {
    id "application"
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
}

group "com.github.lusingander"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "io.javalin:javalin:3.7.0"
    implementation "org.slf4j:slf4j-simple:1.7.28"
    implementation "com.github.kittinunf.fuel:fuel:2.3.1"
    implementation "com.github.kittinunf.fuel:fuel-jackson:2.3.1"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.10.1"
    implementation 'com.github.lusingander:kraphql-github:v0.0.10'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

application {
    mainClassName = "com.github.lusingander.AppKt"
}

run {
    def properties = new Properties()
    def f = project.rootProject.file("local.properties")
    if (f.exists()) {
        properties.load(f.newDataInputStream())
        environment "GITHUB_API_TOKEN", properties.getProperty("github.api.token")
    }
}

task stage(type: Copy, dependsOn: [clean, build])
build.mustRunAfter clean

jar {
    manifest {
        attributes 'Main-Class': 'com.github.lusingander.AppKt'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}